{
  config,
  pkgs,
  lib,
  ...
}:
let
  username = config.nx.username;
  kubeDir = "${config.home-manager.users.${username}.home.homeDirectory}/.config/kube";
  isLinux = pkgs.stdenv.hostPlatform.isLinux;
  isDarwin = pkgs.stdenv.hostPlatform.isDarwin;
in
{
  options = {
    nx.programs.kubetools.enable =
      lib.mkEnableOption "enables some cli tools for managing kubernetes"
      // {
        default = true;
      };
  };
  config = lib.mkIf config.nx.programs.kubetools.enable (
    lib.mkMerge [
      # Common configuration (both platforms)
      {
        home-manager.users.${username} = {
          home.packages = with pkgs; [
            fluxcd
            flux-local
            flate
            hubble
            krew
            kubectl
            kubectl-cnpg
            kubelogin
            kubelogin-oidc
            kubernetes-helm
            kustomize
          ];
          home.sessionVariables = {
            KUBECONFIG = "${kubeDir}/config";
          };
        };
      }

      # Linux: system-level sops with admin and agents kubeconfig
      (lib.mkIf isLinux {
        # admin kubeconfig
        sops.secrets.kubeconfig = {
          owner = username;
          mode = "0600";
          path = "${kubeDir}/config";
          sopsFile = ./secrets/kubeconfig.sops.yaml;
        };
        # agents readonly kubeconfig
        sops.secrets.agents-kubeconfig = {
          owner = username;
          mode = "0600";
          path = "${kubeDir}/agents-config";
          sopsFile = ./secrets/kubeconfig.sops.yaml;
        };
        system.activationScripts.kubeConfigFolderPermissions = ''
          mkdir -p ${kubeDir}
          chown ${username}:users ${config.home-manager.users.${username}.home.homeDirectory}/.config
          chown ${username}:users ${kubeDir}
        '';
      })

      # Darwin: home-manager sops with work kubeconfigs only
      (lib.mkIf isDarwin {
        home-manager.users.${username}.sops.secrets = {
          workkube = {
            path = "${kubeDir}/config";
            sopsFile = ./secrets/kubeconfig.sops.yaml;
          };
          workreadonlykube = {
            path = "${kubeDir}/agents-config";
            sopsFile = ./secrets/kubeconfig.sops.yaml;
          };
          kubeconfig = {
            path = "${kubeDir}/config-home";
            sopsFile = ./secrets/kubeconfig.sops.yaml;
          };
        };
      })
    ]
  );
}
