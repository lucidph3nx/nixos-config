{
  pkgs,
  inputs,
  config,
  lib,
  ...
}:
let
  username = config.nx.username;
in
{
  imports = [
    ../../modules
  ];

  nx.username = "bensherman";

  users.users.${username} = {
    home = "/Users/${username}";
  };

  networking.hostName = "m4mac";

  # Module configuration using nx namespace (matching NixOS pattern)
  nx = {
    programs = {
      prism = {
        profile.default = "standard";
        agent.isolation.default = "sandbox-exec";
        sandboxExecConcurrencyCap = 80;
        pi.atlassian = {
          enable = true;
          defaultCloudId = "08986a80-a6ed-4480-ae2d-4a439d50d71b";
        };
        # Notion MCP, scoped to the Obsidian vault. Scoping keeps ~10 Notion
        # tool schemas out of the system prompt of every code-repo session,
        # keeps a full workspace read/write grant away from agents that have
        # no use for it, and cuts the number of sessions competing to rotate
        # the shared refresh token. See pi/extensions/notion/UPSTREAM.md.
        pi.notion = {
          enable = true;
          repos = [ "~/Documents/obsidian" ];
        };
        # Grafana MCP, reusing the same instance (and sops bundle) as
        # navi/tui. Readable under sandbox-exec since issue #2746 admitted
        # grafana_config_home to the secrets.d allowlist.
        pi.grafana = {
          enable = true;
          config = "home";
        };
        projects.isolationOverrides = {
          "~/Documents/obsidian" = "host";
        };
      };
      # Disable programs that default to enabled but aren't needed/available on Darwin
      podman.enable = false;
      dragon-drop.enable = false;
      nh.enable = false;
      chromium.enable = false; # Not available on Darwin
      bitwarden.enable = false; # Use Homebrew cask instead
      discord.enable = false; # Not commonly used on Darwin
      gimp.enable = false; # Not commonly used on Darwin
      signal.enable = false; # Not available on Darwin
      vimiv.enable = false; # Linux-only (Qt GPU dependencies)
      mpv.enable = false;
      zathura.enable = false;
      ssh.enableWorkKeys = true;
      homeAutomation.enable = true;
      aws.enable = true;
      gitlab-cli.enable = true;
      qutebrowser.enable = false; # broken, due to https://github.com/NixOS/nixpkgs/issues/514179
      tailscaleClient.enable = true;
    };
    desktop = {
      theme = "edge";
      rofi.enable = true; # For shopping list script (uses choose on Darwin)
    };
    services = {
      alloy.enable = true;
      flakeUpdateNotifier.enable = true;
      prismExporter.enable = true;
      syncthing = {
        enable = true;
        obsidian.enable = true;
      };
    };
  };

  # Darwin-specific packages not in shared modules
  environment.systemPackages =
    with pkgs;
    [
      _1password-cli
      arping
      gnutar
      harlequin
      mariadb
      podman # darwin doesn't use virtualisation.podman
      postgresql
      rustup
      tree
      tridactyl-native
      utm
    ]
    ++ [ macron-send ];

  security.sudo.extraConfig = ''
    ${username} ALL=(ALL:ALL) NOPASSWD: ALL
  '';

  services = {
    karabiner-elements.enable = false;
  };

  system.primaryUser = username;

  system.defaults = {
    finder = {
      AppleShowAllExtensions = true;
      AppleShowAllFiles = true;
    };
    dock = {
      autohide = true;
      # basically, permanantly hide dock
      autohide-delay = 1000.0;
      orientation = "left";
    };
    menuExtraClock = {
      IsAnalog = false;
      Show24Hour = true;
      ShowAMPM = false;
      ShowSeconds = true;
    };
    spaces = {
      spans-displays = false;
    };
    universalaccess = {
      reduceMotion = true;
      reduceTransparency = true;
    };
    NSGlobalDomain = {
      _HIHideMenuBar = false;
      InitialKeyRepeat = 14;
      KeyRepeat = 1;
      AppleInterfaceStyle = "Dark";
      AppleICUForce24HourTime = true;
      AppleMeasurementUnits = "Centimeters";
      AppleMetricUnits = 1;
      AppleTemperatureUnit = "Celsius";
      NSWindowShouldDragOnGesture = true;
      NSAutomaticWindowAnimationsEnabled = false;
    };
    # Fix CMD+Q not working in Electron apps (Plexamp)
    # This is a workaround for Electron apps not properly handling macOS keyboard shortcuts
    # See: https://github.com/electron/electron/issues/7165
    CustomUserPreferences = {
      "tv.plex.plexamp" = {
        NSUserKeyEquivalents = {
          "Quit Plexamp" = "@q";
        };
      };
    };
  };

  homebrew = {
    enable = true;
    taps = [
      "datadog-labs/pack"
      "nikitabobko/tap"
    ];
    onActivation = {
      autoUpdate = true;
      cleanup = "uninstall";
      upgrade = true;
      extraFlags = [
        "--force-cleanup"
      ];
    };
    brews = [
      "node"
      "ripgrep" # for plenary in neovim, it can't find the nix binary
      "python"
      "vfkit"
      "datadog-labs/pack/pup"
    ];
    casks = [
      "bitwarden"
      "firefox"
      "karabiner-elements"
      "nikitabobko/tap/aerospace"
      "raycast"
      "scroll-reverser"
    ];
  };

  # Activation scripts
  system.activationScripts.extraActivation.text = ''
    # Set Cmd+Q shortcut for Plexamp (Electron app workaround)
    # Run as user since defaults needs to write to user preferences
    sudo -u ${username} /usr/bin/defaults write tv.plex.plexamp NSUserKeyEquivalents -dict-add "Quit Plexamp" "@q"
  '';

  # Home Manager configuration
  home-manager = {
    useGlobalPkgs = true;
    useUserPackages = true;
    extraSpecialArgs = {
      inherit inputs;
    };
    users.${username} = { config, ... }: {
      programs.chromium = {
        enable = false;
        package = pkgs.hello; # override Linux-only default so Darwin eval doesn't fail
      };
      # launchd user agent to supervise borders (jankyborders) with automatic restart
      launchd.agents.jankyborders = {
        enable = true;
        config = {
          ProgramArguments = [
            "${pkgs.jankyborders}/bin/borders"
            "active_color=0xffa7c080"
            "inactive_color=0x00232a2e"
            "width=10"
          ];
          KeepAlive = true;
          RunAtLoad = true;
          StandardOutPath = "/tmp/jankyborders.log";
          StandardErrorPath = "/tmp/jankyborders.log";
        };
      };
      # launchd user agent to run macron-type socket server in the GUI session
      # so CGEventPost has the correct session context
      launchd.agents.macron-type = {
        enable = true;
        config = {
          ProgramArguments = [ "${pkgs.macron-type}/bin/macron-type" ];
          KeepAlive = true;
          RunAtLoad = true;
          StandardOutPath = "/tmp/macron-type.log";
          StandardErrorPath = "/tmp/macron-type.log";
        };
      };
      home = {
        username = username;
        homeDirectory = "/Users/${username}";
        stateVersion = "25.11";

        sessionPath = [
          "/opt/homebrew/bin"
        ];

        file = {
          ".config/karabiner/karabiner.json".source = ./files/karabiner.json;
          ".config/aerospace/aerospace.toml".source = ./files/aerospace.toml;
          # Bridge the activation-script brew bundle's tap-trust DB path
          # (~/.homebrew/trust.json — the fallback used when XDG_CONFIG_HOME is
          # stripped by sudo --preserve-env=PATH) to the interactive
          # `brew trust` write location (~/.config/homebrew/trust.json).
          # Out-of-store so interactive `brew trust X` can still write to it.
          # See issue #2268 for the full kōrero.
          ".homebrew".source = config.lib.file.mkOutOfStoreSymlink "${config.xdg.configHome}/homebrew";
        };
      };
    };
  };

  system.stateVersion = 5;
}
